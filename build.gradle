import org.apache.tools.ant.taskdefs.Delete;
import org.apache.tools.ant.taskdefs.Zip;
import org.apache.tools.ant.types.ZipFileSet;
import org.apache.tools.ant.types.FileSet;
import org.apache.tools.ant.types.resources.FileResource;
import groovy.xml.MarkupBuilder;

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'checkstyle'

version = '1.1'
tasks.withType(Compile) {
    options.encoding = 'UTF-8'
}

mainClassName = 'de.tntinteractive.portalsammler.Portalsammler'
sourceCompatibility = '1.7'

repositories {
    mavenCentral()
}

configurations {
    classycle
}

dependencies {
    compile 'org.seleniumhq.selenium:selenium-htmlunit-driver:2.37.1', 
            'org.seleniumhq.selenium:selenium-support:2.37.1',
            'org.bouncycastle:bcprov-jdk15on:1.49',
            'org.swinglabs:pdf-renderer:1.0.5',
            'com.jgoodies:jgoodies-forms:1.6.0',
            'com.google.zxing:core:2.2',
            'com.google.zxing:javase:2.2',
            'com.github.sarxos:webcam-capture:0.3.9'
    testCompile 'junit:junit:4.+'
    classycle 'org.specs2:classycle:1.4.1'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Portalsammler', 
            'Implementation-Version': version, 
            'Main-Class': project.mainClassName,
            'Application-Name': 'Portalsammler',
            'Permissions': 'all-permissions'
    }
}


task noCyclicDependencies(type: JavaExec) {
    main = 'classycle.dependency.DependencyChecker'
    classpath = configurations.classycle
    args '-dependencies=check absenceOfPackageCycles > 1 in *', "${project.buildDir}/classes/main"
}
check.dependsOn noCyclicDependencies


task mergeJarsIntoOne(dependsOn: installApp) {
    String libdir = "${project.buildDir}/install/Portalsammler/lib"
    File output = file("${project.buildDir}/oneSignedJar/${project.name}-${project.version}.tmp.jar")
    
    inputs.dir libdir
    outputs.file output
    
    doLast {
        
        FileSet sourceJars = ant.fileset(dir : file(libdir)) {
        }
        sourceJars.setIncludes("*.jar");
    
        ant.zip(destFile: output, 
                encoding: 'UTF-8',
                duplicate: 'preserve') {
            zipfileset(
                src: file("$libdir/${project.name}-${project.version}.jar"),
                includes: '**/META-INF/**'
            );
            
            for (FileResource filename in sourceJars) {
                zipfileset(
                    src: filename,
                    excludes: '**/META-INF/**'
                );
            }
        };
    }
}


task repack200(dependsOn: mergeJarsIntoOne, type: Exec) {
    String injar = "${project.buildDir}/oneSignedJar/${project.name}-${project.version}.tmp.jar";
    String outjar = "${project.buildDir}/oneSignedJar/${project.name}-${project.version}.repacked.jar";
    
    inputs.file injar
    outputs.file outjar
    
    commandLine System.getenv('JAVA_HOME') + '/bin/pack200', '--repack', outjar, injar
}

task signJar(dependsOn: repack200, type: Exec) {
    String injar = "${project.buildDir}/oneSignedJar/${project.name}-${project.version}.repacked.jar";
    String outjar = "${project.buildDir}/oneSignedJar/${project.name}-${project.version}.jar";
    
    inputs.file injar
    outputs.file outjar
    
    commandLine System.getenv('JAVA_HOME') + '/bin/jarsigner', 
        '-storetype', 'pkcs12',
        '-keystore', "${project.buildDir}/../../codesigning.p12", 
        '-tsa', 'http://time.certum.pl',
        '-signedjar', outjar,
        injar,
        'root ca id von tobias baum'
        
    doFirst {
        String password = javax.swing.JOptionPane.showInputDialog('Enter keystore password');
        args '-storepass', password
    }
}

task pack200(dependsOn: signJar, type: Exec) {
    String jar = "${project.buildDir}/oneSignedJar/${project.name}-${project.version}.jar";
    String packgz = "${project.buildDir}/oneSignedJar/${project.name}-${project.version}.jar.pack.gz";
    
    inputs.file jar
    outputs.file packgz
    
    commandLine System.getenv('JAVA_HOME') + '/bin/pack200', packgz, jar
}

build.dependsOn pack200

task createJnlp  {
    File output = file("${project.buildDir}/oneSignedJar/${project.name}-${project.version}.jnlp")
    
    outputs.file output
    
    doLast {
        def out = new FileWriter(output)
        def b = new MarkupBuilder(out)
        b.jnlp(id: 'portalsammler', border: 0) {
            information {
                title('Portalsammler')
                vendor('Tobias Baum / TNT Interactive')
                homepage('http://github.com/tobiasbaum/Portalsammler')
                'offline-allowed'()
            }
            security {
                'all-permissions'()
            }
            resources {
                j2se(version: '7.0+')
                jar(href: "${project.name}-${project.version}.jar")
            }
            'application-desc'()
        }
        out.close();
    }
}
build.dependsOn createJnlp

